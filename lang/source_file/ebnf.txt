
program := declaration* EOF
    ;

declaration :=  funDeclStmt
    | varDeclStmt
    | statement
    ;

funDeclStmt := "fun" function
    ;

function := IDENTIFIER "(" parameters? ")" block
    ;

parameters := IDENTIFIER ( "," IDENTIFIER )*
    ;

varDeclStmt := "var" IDENTIFIER ( "=" expression )? ";"
    ;

statement := exprStmt
    | ifStmt
    | printStmt
    | returnStmt
    | whileStmt
    | block;
    ;

returnStmt := "return" expression? ";"
    ;

whileStmt := "while" "(" expression ")" statement
    ;

ifStmt := "if" "(" expression ")" statement ( "else" statement )?
    ;

printStmt := "print" expression ";"
    ;

block := "{" declaration* "}"
    ;

exprStmt := expression ";"
    ;
    
expression := assignment
    ;

/* Here IDENTIFIER is a VariableExpression */
assignment :=
    IDENTIFIER "=" assignment
    | logic_or
    ;

logic_or := logic_and ( "or" logic_and )*
    ;

logic_and := equality ( "and" equality )*
    ;

equality := comparison ( ( "!=" | "==" ) comparison )*
    ;

comparison := term ( ( ">" | ">=" | "<" | "<=" ) term )*
    ;

term := factor ( ( "-" | "+" ) factor )*
    ;

factor := unary ( ( "/" | "*" ) unary )*
    ;

unary := ( "!" | "-" ) unary | call
    ;

call := primary ( "(" arguments? ")" )*
    ;

arguments := expression ( "," expression )*
    ;

primary := NUMBER
    | STRING
    | "true"
    | "false"
    | "nil"
    | "(" expression ")"
    | IDENTIFIER
    ;